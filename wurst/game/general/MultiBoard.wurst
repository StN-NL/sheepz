package MultiBoard
import Board
import ConfigGame
import LinkedList
import Utils

DynamicCellValue<int> array[24] LIVES
DynamicCellValue<int> array[24] POINTS

public function player.getLives() returns int
  return LIVES[this.getId()].value

public function player.getPoints() returns int
  return POINTS[this.getId()].value

public function player.setLives(int value)
  LIVES[this.getId()].updateValue(value)

public function player.setPoints(int value)
  POINTS[this.getId()].updateValue(value)

public function player.addLives(int value)
  this.setLives(this.getLives() + value)

public function player.addPoints(int value)
  this.setPoints(this.getPoints() + value)

public function initMultiboard()
  let board = new Board("Sheepz (Deluxe Edition)".color(PINK), 0.2)
  
  let columns = asList(
    new BoardColumn("Player".color(COLOR_GOLD), 0.4),
    new BoardColumn("Points".color(COLOR_GOLD), 0.3)
  )
  if GAME_MODE == GameMode.SURVIVAL
    columns.add(new BoardColumn("Lives".color(COLOR_GOLD), 0.3))

  board.columns(columns)

  for ii = 0 to MAX_PLAYERS - 1
    let p = players[ii]
    if p.isIngame() or isSinglePlayer()
      POINTS[ii] = dynamicCell<int>(0, i -> i.toString())
      LIVES[ii] = dynamicCell<int>(SHEEP_MAX_LIVES, i -> i.toString())
      board.addRow()
        ..addCell(p.getName())
        ..addDynamic(POINTS[ii])
        ..addDynamic(LIVES[ii])

  board.show()
