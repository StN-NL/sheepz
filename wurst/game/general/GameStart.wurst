package GameStart
import ConfigWeapons
import ClosureTimers
import DraftWeapon
import EntityManagement
import GameEnd
import MenuFrame
import Miscellaneous
import MyAssets
import Players
import SheepEntity
import Utils
import Weapons

constant GAME_START_DELAY = 1.

init
  doAfter(0.025) ->
    updateMap(-8, 8, 12, 12, Minimaps.tropicalIsland)
    FogMaskEnableOff()
    FogEnableOff()
    initWeapons()
    addListenersChat()
    addQuests()
    startEntityLoop()
    new MenuSetup() -> 
      printWelcome()
      initMultiboard()
      initWeaponDraft()
      enableGameEndCondition()
      setupPlayers()
      addLeaveListener()


function printWelcome()
  print(SLOGAN, 5)
  print("\nFor more information, visit the quest log.", 5)


function setupPlayers()
  for p in getPlayersIngame()
    setupPlayer(p)
    
  if isSinglePlayer()
    targetPractice()


function setupPlayer(player p)
  doAfter(GetRandomReal(0, GAME_START_DELAY)) -> 
    new SheepEntity(p)

    doAfter(GAME_START_DELAY) -> 
      p.addStartingWeapons()


function targetPractice()
  for ii = 1 to 5
    doAfter(GetRandomReal(0, GAME_START_DELAY)) -> 
      new SheepEntity(players[ii])


function addLeaveListener()
  onPlayerLeave() p ->
    if p.getSheep() != null
      p.getSheep().terminate()
