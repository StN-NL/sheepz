package RiotShield
import Fx2Entity
import ConfigWeapons
import HashMap
import MyAssets
import MyUnitEntity


public function MyUnitEntity.riotShield()
  RiotShield.get(this).duration += riotShield.duration
  

class RiotShield extends Fx2Entity
  private static constant map = new HashMap<MyUnitEntity, thistype>

  MyUnitEntity e
  vec3 origin = vec3(-15, 0, 0)
  real duration = 0.

  construct(MyUnitEntity e)
    super(e.owner, e.pos, angle(0), "")
    this.e = e
    removeEffect()

  protected static function get(MyUnitEntity e) returns thistype
    if not map.has(e)
      map.put(e, new RiotShield(e))
      
    return map.get(e)

  override function update()
    if fx.eff == null and duration <= 0
      DoNothing()
    else if fx.eff == null
      createEffect()
    else if duration > 0
      duration -= ANIMATION_PERIOD
      updateEffect()
    else
      SOUND_RIOT_SHIELD.playOnPoint(e.pos)
      removeEffect()

  private function createEffect()
    e.shield()
    SOUND_RIOT_SHIELD.playOnPoint(e.pos)
    setFx(MyModels.riotShield)
    fx.eff.setScale(2.5)
      
  private function updateEffect()
    let facing = e.actor.getFacingAngle()
    setPos(e.origin + e.getPos().offset2d(facing, 80))
    fx.eff.setYaw(facing - 90..asAngleDegrees())

  private function removeEffect()
    e.unshield()
    fx.eff.setAlpha(0)
    fx.eff.destr()
    fx.eff = null
    